// Author: Richard Bradford

plugins {
    id 'java'
    id 'com.moowork.node' version "1.2.0"
}

task webpack(type: Exec) {
    inputs.file("$projectDir/src/main/nodejs/package-lock.json").withPathSensitivity(PathSensitivity.RELATIVE)
    inputs.dir("$projectDir/src/main/nodejs/app").withPathSensitivity(PathSensitivity.RELATIVE)
    outputs.dir("$buildDir/resources/main/static/assets")
    outputs.cacheIf {true}

    commandLine "$projectDir/src/main/nodejs/node_modules/.bin/webpack", "--config", "$projectDir/src/main/nodejs/webpack.config.js"
}

jar.dependsOn webpack

node {
    workDir = file("$buildDir/nodejs")
    nodeModulesDir = file("$projectDir/src/main/nodejs")
}

task testJS(type: NodeTask, dependsOn: ['npmInstall'], description: 'run JS tests') {
    script = file("$projectDir/src/main/nodejs/node_modules/jest/bin/jest.js")
    workingDir = file("$projectDir/src/main/nodejs")
}

test.dependsOn testJS

